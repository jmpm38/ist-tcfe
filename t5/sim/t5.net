*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 10.0
Vee vee 0 -10.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

X1 3 2 vcc vee 4 uA741


C1 in 3 220n

R1 3 0 1k


*R3 results from 1 100k resistor in series with 2 100k resistors in parallel
R3 2 4 150k
R4 2 0 1k


R2 4 out 1k
C2 out 0 110n

*C2 results from 2 220nF capacitors in series

.op
.end

.control




op

print all


ac dec 10 10 1MEG
hardcopy vo1f.ps vdb(out) 
hardcopy vo2f.ps 180/PI*phase(v(out))







let gain = db(v(out)/v(in))
hardcopy gain.ps gain
echo gain_FIG



meas AC maximo MAX vdb(out) from=10 to=100MEG
meas AC freq MAX_AT vdb(out) from=10 to=100MEG



let cost = 14057.88
let cdeviation=abs(1000-freq)
let gaindeviation=abs(100-10^(maximo/20))
let merit = 1/(cost*(gaindeviation+cdeviation +1e-6))



*input impedance in ohm
echo "Zin_TAB"
let Zr_in= Re(-v(in)[40]/i(Vin)[40])
let Zim_in= Im(-v(in)[40]/i(Vin)[40])
echo "Zin = $&Zr_in + $&Zim_in j"
echo "Zin_END"




echo "resultados_TAB"
echo "Central Frequency (Hz)= $&freq"
echo "Gain (dB)= $&maximo"
echo "Central frequency deviation=$&cdeviation"
echo "Gain deviation=$&gaindeviation"
echo "resultados_END"



echo "**************"
echo "custo_TAB"
echo "Cost = $&cost"
echo "Merit = $&merit"
echo "custo_END"
echo "**************"

quit
.endc 
.end
